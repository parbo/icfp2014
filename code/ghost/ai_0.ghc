; Strategy: chase/evade

; Get this ghost's index
; and store it in memory [200]
int 3
mov [200],a

; Get current state
; vitality  -> [201]
; direction -> [202]
int 6
mov [201],a
mov [202],b

; Get current position
; x -> [203]
; y -> [204]
mov a,[200]
int 5
mov [203],a
mov [204],b

; Get lambda man position
; x -> [205]
; y -> [206]
int 1
mov [205],a
mov [206],b

; Find horizontal position relative lambda man
; Horizontal distance to lambda man -> [207]
; Left of lambda man:  0 -> [208]
; Right of lambda man: 1 -> [208]
mov [208],0
mov [207],[205]
sub [207],[203]

jlt #vertpos,[203],[205]
mov [208],1
mov [207],[203]
sub [207],[205]

; Find vertical position relative lambda man
; Vertical distance to lambda man -> [209]
; Above lambda man: 0 -> [210]
; Below lambda man: 1 -> [210]

vertpos:
mov [210],0
mov [209],[206]
sub [209],[204]
jlt #distance,[204],[206]
mov [210],1
mov [209],[204]
sub [209],[206]

; Find greatest distance
; Horizontal distance is greater: 0 -> [211]
; Vertical distance is greater:   2 -> [211]

distance:
mov [211],0
add [211],[208]  ; Use horizontal move (in normal mode)
mov [212],2
add [212],[210]  ; Use vertical move as alternative
jgt #move,[207],[209]
mov [211],2
add [211],[210]  ; Use vertical move (in normal mode)
mov [212],0
add [212],[208]  ; Use horizontal move as alternative

; Select move
move:

; If invisible, do nothing
jeq #halt,[201],2

; Movement table for fright mode (opposite of normal mode)
mov [0],3  ; Left
mov [1],1  ; Right
mov [2],0  ; Up
mov [3],2  ; Down

; If fright mode, use the table above
jeq #trymove,[201],1

; Movement table for normal mode
mov [0],1  ; Right
mov [1],3  ; Left
mov [2],2  ; Down
mov [3],0  ; Up

; Try the move
trymove:
mov c,[211]
mov d,[c]

; New position
mov a,[203]  ; x
mov b,[204]  ; y
mov e,d
and e,1
jeq #vertical_move,e,0
; Horizontal move
sub a,d
add a,2
jeq #test_valid_move,0,0
vertical_move:
add b,d
sub b,1

test_valid_move:
int 7
jeq #alternate_move,a,0  ; Is the new position a wall?

; Table of opposite directions
mov [4],2  ; Down
mov [5],3  ; Left
mov [6],0  ; Up
mov [7],1  ; Right

mov e,4
add e,d

; Trying to change to opposite direction?
jeq #alternate_move,[e],[202]

jeq #domove,0,0

alternate_move:
mov c,[212]
mov d,[c]

domove:
mov a,d
int 0

jeq #halt,0,0

; Send debug output
; a: index
; b: vitality
; c: direction
; d: x pos
; e: y pos
; f: lambda man x pos
; g: lambda man y pos
; h: not defined
debug:
mov a,[200]
mov b,[201]
mov c,[202]
mov d,[203]
mov e,[204]
mov f,[205]
mov g,[206]
;mov h,0
int 8

halt:
hlt
